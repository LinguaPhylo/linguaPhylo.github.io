# Language features

The LinguaPhylo language has two main components: Values (which can be either constant or random variables, and either 
objects or primitives) and Generators (which can be either generative distributions, deterministic functions or method 
calls). Equivalently a LinguaPhylo model describes a directed acyclic graph made up of two main types of nodes (values 
and generators).

## Values

A Value is either a primitive (Boolean, Integer, Double, String and arrays thereof), or an object (e.g. TimeTree, Taxa, 
Alignment). In addition values generated by a generative distribution are known as random variables.

### Primitive constants

The simplest type of value in LinguaPhylo is a primitive constant such in the following example. The first line defines 
a constant named `a` with a value of `2.0`. There is no explicit statement of the type, which is simply inferred to be
a Double because the literal string `2.0` has a fractional component. The second constant `b` here is a constant of 
type Integer because it has no decimal point or fractional component. The third constant is of primitive type Boolean, 
inferred by the keyword `true`. the last constant is of type String because of the double quotes surrounding the 
literal value.

```
a = 2.0;
b = 2;
c = true;
d = "lphy";
```

### Arrays

The notion of primitives can be extended to arrays. To declare a constant array a list of comma-delimited 
literals is enclosed in square brackets as shown below. For sequences of consecutive integers a more compact notation can be 
employed using the `:' to define a range. In the example below a and b represent the same constant array containing the 
consecutive integers from 1 to 10 inclusive.

```
  a = [1,2,3,4,5,6,7,8,9,10];
  b = 1:10;
```           

## Generators

Generators are capable of generating values. There are three kinds of generators in LPhy: generative distributions, 
deterministic functions and method calls. They are each described in greater detail below.

### Generative distributions

A generative distribution (henceforth referred to simply as distribution) generates a named random variable which has a 
value that is either an object or a primitive. 
Some distributions have parameters and these parameters are themselves values, which are distinguished in their role in
the distribution by argument labels. In the example below we have a LogNormal distribution which has two arguments with
labels `meanlog` and `sdlog`. These argument labels are fixed attributes of the distribution and can be assigned a
value of the appropriate type. The argument labels and the value types that they can be assigned ar part of the 
definition of the distribution. Below is an example where a random variable named `lambda` is generated by a LogNormal
distribution in which the `meanlog` is set to the literal constant `3.0` and the `sdlog` is set to literal constant 
`1.0`.

```
lambda ~ LogNormal(meanlog=3.0, sdlog=1.0);
```           

### Deterministic functions

A deterministic function (henceforth called simply a function) generates a value that is deterministically derived from
the values of its arguments. If the function has a single argument then no argument label need be specified, otherwise
arguments are labelled in the same way as for distributions. In the code below the function named `hky` takes two 
parameters with argument labels `kappa` and `freq` and deterministically produces an instantaneous rate matrix using
the values assigned to those arguments.

```
Q = hky(kappa=2.0, freq=[0.2, 0.25, 0.4, 0.15]);
```

### Method calls

Values with complex types, such as TimeTree, Alignment and Taxa may have methods associated with them that typically
provide information about the value. The following line returns a named Integer `n` representing the number of nodes in
the value named `tree`. This code will be valid if `tree` is of type TimeTree.

```
n = tree.nodeCount();
```

## Vectorization

LinguaPhylo has built-in vectorization so that any distribution or function can be called with a vectorized form of its 
arguments to produce multiple values from the generator. The first line in the example below will produce an array of
doubles of length 3. The first double will be a random variable drawn from a lognormal with `meanlog=1.0` and 
`sdlog=1.0`. The second element of the array will be a random variable drawn from a lognormal with `meanlog=2.0` and 
`sdlog=1.0`, and so on. The second line will produce and array of two instantaneous rate matrices. The first will be 
be constructed with a kappa of 2.0, and the second will be constructed with a kappa of 3.0.

```
lambda ~ LogNormal(meanlog=[1.0, 2.0, 3.0], sdlog=1.0);  
Q = hky(kappa=[2.0,3.0], freq=[0.2, 0.25, 0.4, 0.15]);
```           
